plugins {
    id 'groovy'
}

ext.pluginClassNames = [
    'gaddman.rundeck.plugins.servicenow.ServiceNowTaskView',
    'gaddman.rundeck.plugins.servicenow.ServiceNowTaskAddNote',
    'gaddman.rundeck.plugins.servicenow.ServiceNowTaskSetState',
    'gaddman.rundeck.plugins.servicenow.ServiceNowCommunity',
].join(',')

defaultTasks 'clean','build'

java {
    sourceCompatibility = JavaVersion.VERSION_11
    targetCompatibility = JavaVersion.VERSION_11
}

repositories {
    mavenLocal()
    mavenCentral()
}

configurations{
    pluginLibs

    implementation{
        extendsFrom pluginLibs
    }
}

dependencies {
    implementation 'org.rundeck:rundeck-core:5.0.0-20231216'
    implementation 'org.codehaus.groovy:groovy-all:3.0.9'
    implementation 'org.apache.httpcomponents:httpclient:4.5.14'
}

// Task to copy plugin libs to output/lib dir.
task copyToLib(type: Copy) {
    into "$buildDir/output/lib"
    from configurations.pluginLibs
}

jar {
    from "$buildDir/output"
    manifest {
        def libList = configurations.pluginLibs.collect{'lib/'+it.name}.join(' ')

        attributes 'Rundeck-Plugin-Classnames': pluginClassNames
        attributes 'Rundeck-Plugin-File-Version': '1.0.0'
        attributes 'Rundeck-Plugin-Name': 'ServiceNow Community Plugins'
        attributes 'Rundeck-Plugin-Description': 'Set of plugins to work with ServiceNow'
        attributes 'Rundeck-Plugin-Author': 'Chris Gadd'
        attributes 'Rundeck-Plugin-Rundeck-Compatibility-Version': '5.x+'
        attributes 'Rundeck-Plugin-Tags': 'java,WorkflowStep'
        attributes 'Rundeck-Plugin-License': 'GNU Lesser General Public License version 3'
        attributes 'Rundeck-Plugin-Source-Link': 'https://github.com/gaddman/rundeck-servicenow-plugin'
        attributes 'Rundeck-Plugin-Target-Host-Compatibility': 'all'
        attributes 'Rundeck-Plugin-Version': '1.2'
        attributes 'Rundeck-Plugin-Archive': 'true'
        attributes 'Rundeck-Plugin-Libs': "${libList}"

    }
    dependsOn(copyToLib)
}
